num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;

num trunkHeight = 10 + randomInt(10);
num trunkBase = 4 + randomInt(3);
num trunkRadius = 2;
str mainBlock = "minecraft:obsidian";
str altBlock = "minecraft:black_concrete";

num trunkMaxReachRadius = 4;

num branchRadius = 1.25;
num sideBranchInterval = 2 + randomInt(2);
num sideBranchMinHeight = 1 + randomInt(3);
num sideBranchLengthMin = 4 + randomInt(3);
num sideBranchLengthVariation = 3;
num sideBranchVerticalVariation = 0;
num sideBranchVerticalDirection = 1 - randomInt(randPrecision) / randPrecision * 2;
num sideBranchLeafRadiusMin = 4;
num sideBranchLeafTranslate = 0;
num sideBranchLeafTopFlatness = 1;
num sideBranchLeafBottomFlatness = 2;
num sideBranchLeafDensity = 0.5;
str sideBranchLeafStructure = "shattered_leaves_clump";

num topBranches = 3 + randomInt(2);
num topBranchLengthUpwardsMin = 7 + randomInt(5);
num topBranchLengthOutwardsMin = 5 + randomInt(5);
num topBranchVerticalDirection = 1;
num topBranchLeafRadiusMin = 5;
num topBranchLeafTranslate = 0;
num topBranchLeafTopFlatness = 1;
num topBranchLeafBottomFlatness = 2;
num topBranchLeafDensity = 0.5;
str topBranchLeafStructure = "shattered_leaves_clump";

str setBlock = mainBlock;
num xOffset = 0;
num zOffset = 0;
// Generate roots at base of trunk
num rootRadius = 1.5;
num roots = 3 + randomInt(2);
num rootMaxLength = 10;
num rootDroop = 0.06;
num rootVerticalDirection = -0.4 - randomInt(randPrecision) / randPrecision * 0.3;
num rootAngle = randomInt(randPrecision) / randPrecision * 2 * pi;
for (num r = 0; r < roots; r = r + 1) {
    // Branch direction vector
    num dx = sin(rootAngle);
    num dy = rootVerticalDirection;
    num dz = cos(rootAngle);
    // Normalize vector
    num mag = sqrt(pow(dx, 2) + pow(dy, 2) + pow(dz, 2));
    dx = dx / mag;
    dy = dy / mag;
    dz = dz / mag;

    for (num i = 0; i <= rootMaxLength; i = i + 0.5) {
        num cx = i * dx;
        num cy = trunkBase + i * dy;
        num cz = i * dz;

        // Place root block
        if (getBlock(cx, cy, cz) == "minecraft:end_stone" ||
            getBlock(cx+1, cy, cz+1) == "minecraft:end_stone" ||
            getBlock(cx, cy, cz+1) == "minecraft:end_stone" ||
            getBlock(cx+1, cy, cz) == "minecraft:end_stone") {
            break;
        }

        for (num x = -rootRadius; x < rootRadius; x = x + 1) {
            for (num y = -rootRadius; y < rootRadius; y = y + 1) {
                for (num z = -rootRadius; z < rootRadius; z = z + 1) {
                    if (pow(x,2)+pow(y,2)+pow(z,2) < pow(rootRadius,2) ) {
                        setBlock = mainBlock;
                        if (randomInt(2) == 0) setBlock = altBlock;
                        block(cx + x, cy + y, cz + z, setBlock);
                    }
                }
            }
        }

        // Point vector down more
        dy = dy - rootDroop;
        // Normalize vector
        mag = sqrt(pow2(dx) + pow2(dy) + pow2(dz));
        dx = dx / mag;
        dy = dy / mag;
        dz = dz / mag;
    }

    rootAngle = rootAngle + goldenAngle;
}

num branchAngle = randomInt(randPrecision) / randPrecision * 2 * pi;
for (num i = 0; i < trunkHeight+trunkBase; i = i + 1) {
    if (i > sideBranchMinHeight) {
        if (i % sideBranchInterval == 0) {
            // Branch direction vector
            num dx = sin(branchAngle);
            num dy = sideBranchVerticalDirection + randomInt(randPrecision) / randPrecision * sideBranchVerticalVariation;
            num dz = cos(branchAngle);
            // Normalize vector
            num mag = sqrt(pow(dx, 2) + pow(dy, 2) + pow(dz, 2));
            dx = dx / mag;
            dy = dy / mag;
            dz = dz / mag;
            // Branch origin
            num ox = xOffset;
            num oy = i + trunkBase;
            num oz = zOffset;
            // Set branch block based on largest unsigned vector component
            num branchLength = sideBranchLengthMin + randomInt(randPrecision) / randPrecision * sideBranchLengthVariation;
            // Generate branch

            for (num l = 0; l <= branchLength; l = l + 1) {
                if (getBlock(l * dx + ox, l * dy + oy, l * dz + oz) == "minecraft:endstone") break;
                for (num x = -branchRadius; x < branchRadius; x = x + 1) {
                    for (num y = -branchRadius; y < branchRadius; y = y + 1) {
                        for (num z = -branchRadius; z < branchRadius; z = z + 1) {
                            if (pow(x,2)+pow(y,2)+pow(z,2) < pow(branchRadius,2) ) {
                                setBlock = mainBlock;
                                if (randomInt(2) == 0) setBlock = altBlock;
                                block(l * dx + ox + x, l * dy + oy + y, l * dz + oz + z, setBlock);
                            }
                        }
                    }
                }
            }
            // Rotate angle of next branch
            branchAngle = branchAngle + goldenAngle;
            // Generate leaf cluster at the end of the branch
            num radius = sideBranchLeafRadiusMin;
            num radiusSquared = pow(radius, 2);
            // Leaf cluster origin (end of branch)
            num lox = ox + branchLength * dx;
            num loy = oy + branchLength * dy + sideBranchLeafTranslate;
            num loz = oz + branchLength * dz;
            for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
                for (num cy = ceil(-radius / sideBranchLeafBottomFlatness); cy <= ceil(radius / sideBranchLeafTopFlatness); cy = cy + 1) {
                    for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                        num squashFactor = sideBranchLeafBottomFlatness;
                        if (cy > 0) squashFactor = sideBranchLeafTopFlatness;
                        num distanceSquared = pow(cx, 2) + pow(cy * squashFactor, 2) + pow(cz, 2);
                        if (distanceSquared < radiusSquared && randomInt(randPrecision) / randPrecision < sideBranchLeafDensity) {
                            structure(lox + cx, loy + cy, loz + cz, sideBranchLeafStructure, "NONE", "CW_90", "CCW_90", "CW_180");
                        }
                    }
                }
            }
        }
    }
    for (num x = -trunkRadius; x < trunkRadius; x = x + 1) {
        for (num y = -trunkRadius; y < trunkRadius; y = y + 1) {
            for (num z = -trunkRadius; z < trunkRadius; z = z + 1) {
                if (pow(x,2)+pow(y,2)+pow(z,2) < pow(trunkRadius,2) ) {
                    setBlock = mainBlock;
                    if (randomInt(2) == 0) setBlock = altBlock;
                    block(x + xOffset, i + y, z + zOffset, setBlock);
                }
            }
        }
    }

    xOffset = xOffset + (1 - randomInt(3));
    zOffset = zOffset + (1 - randomInt(3));

    if (xOffset > trunkMaxReachRadius) xOffset = xOffset - 1;
    if (xOffset < -trunkMaxReachRadius) xOffset = xOffset + 1;
    if (zOffset > trunkMaxReachRadius) zOffset = zOffset - 1;
    if (zOffset < -trunkMaxReachRadius) zOffset = zOffset + 1;

}

for (num b = 1; b <= topBranches; b = b + 1) {
    num t = b / topBranches;
    num ti = 1 - t;
    // Branch direction vector
    num dx = sin(branchAngle) * t;
    num dy = topBranchVerticalDirection;
    num dz = cos(branchAngle) * t;
    // Normalize vector
    num mag = sqrt(pow(dx, 2) + pow(dy, 2) + pow(dz, 2));
    dx = dx / mag;
    dy = dy / mag;
    dz = dz / mag;
    // Branch origin
    num ox = xOffset;
    num oy = trunkHeight + trunkBase;
    num oz = zOffset;
    // Set branch block based on largest unsigned vector component
    num branchLength = topBranchLengthUpwardsMin * ti + topBranchLengthOutwardsMin * t;
    // Generate branch
    for (num l = 0; l <= branchLength; l = l + 1) {
        for (num x = -branchRadius; x < branchRadius; x = x + 1) {
            for (num y = -branchRadius; y < branchRadius; y = y + 1) {
                for (num z = -branchRadius; z < branchRadius; z = z + 1) {
                    if (pow(x,2)+pow(y,2)+pow(z,2) < pow(branchRadius,2) ) {
                        setBlock = mainBlock;
                        if (randomInt(2) == 0) setBlock = altBlock;
                        block(l * dx + ox + x, l * dy + oy + y, l * dz + oz + z, setBlock);
                    }
                }
            }
        }
    }
    // Rotate angle of next branch
    branchAngle = branchAngle + goldenAngle;
    // Generate leaf cluster at the end of the branch
    num radius = topBranchLeafRadiusMin;
    num radiusSquared = pow(radius, 2);
    // Leaf cluster origin (end of branch)
    num lox = ox + branchLength * dx;
    num loy = oy + branchLength * dy + topBranchLeafTranslate;
    num loz = oz + branchLength * dz;
    for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
        for (num cy = ceil(-radius / topBranchLeafBottomFlatness); cy <= ceil(radius / topBranchLeafTopFlatness); cy = cy + 1) {
            for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                num squashFactor = topBranchLeafBottomFlatness;
                if (cy > 0) squashFactor = topBranchLeafTopFlatness;
                num distanceSquared = pow(cx, 2) + pow(cy * squashFactor, 2) + pow(cz, 2);
                if (distanceSquared < radiusSquared && randomInt(randPrecision) / randPrecision < topBranchLeafDensity) {
                    structure(lox + cx, loy + cy, loz + cz, topBranchLeafStructure, "NONE", "CW_90", "CCW_90", "CW_180");
                }
            }
        }
    }
}